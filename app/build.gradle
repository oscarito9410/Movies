apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco-android'


def globalConf = rootProject.ext

kapt {
    generateStubs = true
}

jacoco {
    toolVersion = '0.8.0'
}

androidExtensions {
    experimental = true
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.aboolean.movies"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

android.testOptions {
    unitTests.all {
        jacoco {
            includeNoLocationClasses = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.12'


    Map<String, String> dependencies = globalConf.dependencies;

    implementation dependencies.kotlin
    implementation dependencies.androidX
    implementation dependencies.recyclerView
    implementation dependencies.cardView
    implementation dependencies.supportV4
    implementation dependencies.supportDesign
    implementation dependencies.constraintLayout
    implementation dependencies.navigationFragment
    implementation dependencies.navigationUi


    implementation dependencies.rxJava
    implementation dependencies.rxAndroid

    implementation dependencies.retrofit
    implementation dependencies.retrofitConverterGson
    implementation dependencies.retrofitAdapterRxJava
    implementation dependencies.okhttp
    implementation dependencies.okhttpLogging

    implementation dependencies.stetho
    implementation dependencies.stethoOkhttp
    implementation dependencies.stethoOkhttp


    implementation dependencies.glide
    annotationProcessor dependencies.glideAnnotation
    implementation dependencies.circleImage


    implementation dependencies.koin
    implementation dependencies.lottie


    androidTestImplementation dependencies.testRunner
    androidTestImplementation dependencies.espressoCore
    testImplementation dependencies.robolEletrict
    testImplementation dependencies.mockito
    testImplementation dependencies.mockitoKotlin

    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "com.squareup.retrofit2:converter-moshi:2.3.0"
    implementation "com.squareup.moshi:moshi:1.6.0"
    implementation "com.squareup.moshi:moshi-kotlin:1.6.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.6.0"
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation "org.koin:koin-android-viewmodel:2.0.0"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test:core:1.2.0'

    def room_version = "2.2.3"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // optional - RxJava support for Room
    implementation "androidx.room:room-rxjava2:$room_version"
    // Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    androidTestCompile ("android.arch.core:core-testing:$room_version") {
        exclude module: 'support-compat'
        exclude module: 'support-annotations'
        exclude module: 'support-core-utils'
    }

}


project.afterEvaluate {
    //Gather build type and product flavor names in a list
    def buildTypes = android.buildTypes.collect { type -> type.name }
    def productFlavors = android.productFlavors.collect { flavor -> flavor.name }
    if (!productFlavors) productFlavors.add('')

    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->

            //Define the sourceName and the sourcePath.
            def sourceName, sourcePath
            if (!productFlavorName) {
                sourceName = sourcePath = "${buildTypeName}"
            } else {
                sourceName = "${productFlavorName}${buildTypeName.capitalize()}"
                sourcePath = "${productFlavorName}/${buildTypeName}"
            }

            def testTaskName = "test${sourceName.capitalize()}UnitTest"

            task "create${sourceName.capitalize()}UnitTestCoverageReport"(type: JacocoReport, dependsOn: "$testTaskName") {

                group = "Reporting"
                description =
                        "Generate Jacoco coverage reports on the ${sourceName.capitalize()} build."

                reports {
                    xml.enabled true
                    html.enabled true
                }

                //Directory where the compiled class files are
                classDirectories =
                        fileTree(dir: "${project.buildDir}/intermediates/classes/${sourcePath}",
                                excludes: ['**/R.class',
                                           '**/BR.class',
                                           '**/R$*.class',
                                           '**/*$ViewInjector*.*',
                                           '**/*$ViewBinder*.*',
                                           '**/BuildConfig.*',
                                           'android/**',
                                           '**/Manifest*.*',
                                           '**/*$Lambda$*.*', // Jacoco can not handle several "$" in class name.
                                           '**/*Module.*', // Modules for Dagger.
                                           '**/*Dagger*.*', // Dagger auto-generated code.
                                           '**/*MembersInjector*.*', // Dagger auto-generated code.
                                           '**/*_Provide*Factory*.*',
                                           '**/*_Factory.*', //Dagger auto-generated code
                                           '**/*$*$*.*' // Anonymous classes generated by kotlin
                                ])

                sourceDirectories = files(["src/main/java",
                                           "src/$productFlavorName/java",
                                           "src/$buildTypeName/java"])


                executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")
            }
        }
    }
}